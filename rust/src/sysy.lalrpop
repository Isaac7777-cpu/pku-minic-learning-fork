use crate::c_ast::{Block, CompUnit, FuncDef, FuncType, Stmt, Exp, PrimaryExp, Number, UnaryExp, UnaryOp};

grammar;

match {
  // Skip space and comments
  r"\s*" => {},
  r"//[^\n\r]*[\n\r]*" => {},
  r"/\*([^*]|\*+[^*/])*\*+/" => {},
  // And just use the basic one
  _
}

// Define ComputUnit, with string as return value
// parser would return FuncDef
pub CompUnit: CompUnit = <func_def: FuncDef> => CompUnit { <> };

FuncDef: FuncDef = {
  <func_type: FuncType> <ident: Ident> "(" ")" <block: Block> => {
    FuncDef { <> }
  }
};

FuncType: FuncType = "int" => FuncType::Int;

Block: Block = "{" <stmt: Stmt> "}" => Block { <> };

Stmt: Stmt = "return" <exp: Exp> ";" => Stmt { <> };

Exp: Exp = <unary_exp: UnaryExp> => Exp { unary_exp: Box::new(unary_exp) };

PrimaryExp: PrimaryExp = {
  "(" <exp: Exp> ")" => PrimaryExp::ExpPrimary { exp: Box::new(exp) },
  <num: Number> => PrimaryExp::NumberPrimary { <> },
};

Number: i32 = <num: IntConst> => <>;

UnaryExp: UnaryExp = {
  <primary_exp: PrimaryExp> => UnaryExp::PrimaryUnary{ primary_exp: Box::new(primary_exp) },
  <op: UnaryOp> <e: UnaryExp> => UnaryExp::OpUnaryUnary{ unary_op: op, unary_exp: Box::new(e) }
};

// If you match identifer, return the string
// <> represents the result matched by the regex
Ident: String = r"[_a-zA-Z][_a-zA-Z0-9]*" => <>.to_string();

// Capture the number
IntConst: i32 = {
  r"[1-9][0-9]*" => i32::from_str_radix(<>, 10).unwrap(),
  r"0[0-7]*" => i32::from_str_radix(<>, 8).unwrap(),
  r"0[xX][0-9a-fA-F]+" => i32::from_str_radix(&<>[2..], 16).unwrap(),
}

UnaryOp: UnaryOp = {
  "+" => UnaryOp::PLUS,
  "-" => UnaryOp::MINUS,
  "!" => UnaryOp::BANG,
}
